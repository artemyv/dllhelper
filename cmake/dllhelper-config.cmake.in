include(CMakeFindDependencyMacro)

# Load transitive dependency if the library was built with GSL
if(@DLLHELPER_USE_GSL@)
    find_dependency(Microsoft.GSL CONFIG)
endif()

# Select targets file based on platform/architecture when available
set(_dllhelper_os "${CMAKE_SYSTEM_NAME}")
if(WIN32)
    set(_dllhelper_os "win32")
endif()

set(_dllhelper_arch_tag "")
if(DEFINED CMAKE_GENERATOR_PLATFORM AND CMAKE_GENERATOR_PLATFORM)
    string(TOLOWER "${CMAKE_GENERATOR_PLATFORM}" _dllhelper_genplat)
    if(_dllhelper_genplat STREQUAL "win32")
        set(_dllhelper_arch_tag "x86")
    elseif(_dllhelper_genplat STREQUAL "x64")
        set(_dllhelper_arch_tag "x64")
    elseif(_dllhelper_genplat STREQUAL "arm64")
        set(_dllhelper_arch_tag "arm64")
    endif()
endif()
if(NOT _dllhelper_arch_tag)
    if(CMAKE_SIZEOF_VOID_P EQUAL 4)
        set(_dllhelper_arch_tag "x86")
    else()
        if(CMAKE_SYSTEM_PROCESSOR MATCHES "^(ARM64|aarch64)$")
            set(_dllhelper_arch_tag "arm64")
        else()
            set(_dllhelper_arch_tag "x64")
        endif()
    endif()
endif()

set(_dllhelper_targets_file "${CMAKE_CURRENT_LIST_DIR}/dllhelper-targets-${_dllhelper_os}.${_dllhelper_arch_tag}.cmake")
if(EXISTS "${_dllhelper_targets_file}")
    include("${_dllhelper_targets_file}")
else()
    include("${CMAKE_CURRENT_LIST_DIR}/dllhelper-targets.cmake")
endif()

# Expose variables for parent projects using this package
set(DLLHELPER_OS_DIR "${_dllhelper_os}")
set(DLLHELPER_ARCH_TAG "${_dllhelper_arch_tag}")